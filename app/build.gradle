apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-parcelize'

def versions = [
        kotlin  : '1.4.31',
        androidx: [
                core            : '1.3.2',
                appcompat       : '1.2.0',
                constraintlayout: '2.0.4',
                navigation      : '2.3.4',
                lifecycle       : '2.3.0'
        ],
        material: '1.3.0',
        timber  : '4.7.1',
        koin    : '1.0.2',

        test    : [
                junit: '4.13.2',
                androidx: [
                        junit: '1.1.2',
                        espresso : '3.3.0'
                ]
        ]
]

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.netchar.nicknamer"
        minSdkVersion 23
        targetSdkVersion 30
        versionCode extractVersionCode(appVersionName)
        versionName appVersionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            minifyEnabled false
            applicationIdSuffix '.debug'
            versionNameSuffix '-debug'
        }

        release {
            minifyEnabled true
            applicationIdSuffix '.release'
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        applicationVariants.all { variant ->
            variant.outputs.all {
                outputFileName = "nicknamer_${variant.name}_${variant.versionName}.apk"
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs = ["-Xinline-classes"]
    }
    buildFeatures {
        viewBinding true
    }
}

dependencies {
    kapt "com.android.databinding:compiler:3.1.4"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$versions.kotlin"
    implementation "androidx.core:core-ktx:$versions.androidx.core"
    implementation "androidx.appcompat:appcompat:$versions.androidx.appcompat"
    implementation "com.google.android.material:material:$versions.material"
    implementation "androidx.constraintlayout:constraintlayout:$versions.androidx.constraintlayout"
    implementation "androidx.navigation:navigation-fragment-ktx:$versions.androidx.navigation"
    implementation "androidx.navigation:navigation-ui-ktx:$versions.androidx.navigation"
    implementation "androidx.lifecycle:lifecycle-common-java8:$versions.androidx.lifecycle"
    implementation "com.jakewharton.timber:timber:$versions.timber"
    implementation "org.koin:koin-android:$versions.koin"
    implementation "org.koin:koin-android-viewmodel:$versions.koin"

    // Tests
    testImplementation "junit:junit:$versions.test.junit"
    androidTestImplementation "androidx.test.ext:junit:$versions.test.androidx.junit"
    androidTestImplementation "androidx.test.espresso:espresso-core:$versions.test.androidx.espresso"
}